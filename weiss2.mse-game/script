numeric := filter_text@(match: "^[0123456789]+")
rarity	:= filter_text@(match: "^[CRU]+")
level_f := filter_text@(match: "^[0123]+")
auto_bold :=
{
	temp := input
	# Remove all size tags.
	input := temp
}

remove_kw :=
{
	if contains(input, match: "<i-keyword>") then
		"<sym-auto><i-keyword>" + remove_tags( filter_text(match: "<i-keyword>(.+)</i-keyword>", input) ) + "</i-keyword></sym-auto>"
	else
		"<sym-auto><i-keyword>" + input + "</i-keyword></sym-auto>"
}

color_to_word := [
	g: "green"
	r: "red"
	y: "yellow"
	b: "blue"
]

list_utils := [
	last: { input[length(input) - 1] }
	first: { input[0] }
	join: { substring( for each v in input do ((seperator+v) or else (seperator+v[0])), begin: length(seperator)) }@(seperator: ",")
]

map_utils := [
	keys: { [] + ( for each k:v in input do [k] ) }
]


######################

#
# Reader Notes: Don't experience my pain. Start from the bottom going up.
#
json_start := "\{"
json_end := "\}"
json_array_start := "["
json_array_end := "]"

generate_english_numbers := { [["zero"]] + (for x from 1 to 30 do [[english_number(x)]]) }
english_numbers := generate_english_numbers()

#
# Construct the JSON map, tho it's mostly just going to be an array of cards anyway.
#

is_int := { to_int(input) or else false == input }
is_real := {  to_real(input) or else "wut" == input }
is_string := { input != [] and (to_string(input) or else -1 == input) }

is_primitive := {
	if to_int(input) or else false == input then (
		true
	)
	else if to_real(input) or else "wut" == input then (
		true
	)
	else if to_string(input) or else -1 == input then (
		true
	) else (
		false
	)
}

is_map := {
	result := false
	if is_primitive(input) then (
		result
	) else (
		for each k:v in input do (
			result := true
		)
	)
	result
}

is_list := {
	result := false
	if is_primitive(input) then (
		result
	)
	else if input == [] then (
		result := true
	)
	else (
		for each k:v in input do (
			result := result or ((to_int(k) or else -1) == k)
		)
	)
	result
}

to_json_row := {
	["\"{key}\": {to_json(value)}"]
}

to_map_json := {
	json_start
+	list_utils.join(for each k:v in input do to_json_row(key: k, value: v))
+	json_end
}

to_array_json := {
	json_array_start
+	(if (input != []) then list_utils.join(for each item in input do [to_json(item)]))
+	json_array_end
}

json_escape := {
	a := {input} +
		replace@(match: "\\\\", replace: "\\\\\\") +
		replace@(match: "\\\"", replace: "\\\\\"") +
		replace@(match: "\\t", replace: "\\\\t") +
		replace@(match: "\\r", replace: "\\\\r") +
		replace@(match: "\\n", replace: "\\\\n") +
		{input}
	a(input)
}

to_json := {
	if is_int(input) then
		to_int(input)
	else if is_real(input) then
		to_real(input)
	else if is_string(input) then
		"\"{ json_escape(input) }\""
	## Put JSON Extensions Here ##
	else if is_card(input) then
		to_map_json(to_map_from_card(input))
	else if is_set(input) then
		to_array_json(to_array_from_set(input))
	###
	else if is_list(input) then
		to_array_json(input)
	else if is_map(input) then
		to_map_json(input)
	else
		"\"{ json_escape(input) }\""
}

## Card JSON Extensions ##
is_card := {
	input.cardname or else "" != "" 
}

to_map_from_card := { # (card input)
	[ 	name: [en: input.cardname, jp: ""],
		serial: input.api_serial,
		traits: (if input.trait_one != "" then [input.trait_one] else []) + (if input.trait_two != "" then [input.trait_two] else []),
		type: input.card_type,
		color: input.color,
		side: set.side,
		rarity: input.api_rarity,
		level: position(of: [input.level], in: english_numbers),
		cost: position(of: [input.cost], in: english_numbers),
		soul: position(of: [split_text(input.soul, match: " - ")[0]], in: english_numbers),
		power: input.power,
		triggers: (if input.trigger == "none" then [] else [input.trigger]) + (if input.extra_trigger == "off" then [] else [input.extra_trigger]),
		flavor: input.flavor_text,
		effect: split_text(input.rules_text + "\n" + input.replay_text, match: "\n")
	]
}

is_set := {
	input == set
}

to_array_from_set := { # (set input)
	[] + (for each card in set.cards do [card])
}
	
#####################

sp_auto_fix :=
	remove_tag@(tag: "<color")

auto_fix := 
	# Add Keywords
	# step 1 : remove all automatic tags
	{
		if (length(remove_tags(input)) < 1) then
			""
		else
			input
	} +
	remove_tag@(tag: "<sym-auto>") +
	remove_tag@(tag: "<i-auto>")   +
	remove_tag@(tag: "<b-auto>")   +
	remove_tag@(tag: "<error-spelling") +
	remove_tag@(tag: "<nospellcheck") +
	remove_tag@(tag: "<color") +
	remove_tag@(tag: "<size-auto") +
	remove_tag@(tag: "<font-auto") +
	remove_tag@(tag: "<font") +
	remove_tag@(tag: "<align") +
	remove_tag@(tag: "<value") +
	
	# step 2 : reminder text for keywords
	# before: not contains(card.rules_text, match: "<i-keyword>") // and not used_placeholders
	expand_keywords@(
		condition: { correct_case and not used_placeholders  }, 
		default_expand: { true },
		combine: {
		#	keyword  := "<nospellcheck>{keyword}</nospellcheck>"
		#	reminder := process_english_hints(reminder)
			if mode == "trigger" then "<i>({reminder})</i>"
			#	else "<i>(</i><hidden>.</hidden><i>{reminder})</i>"
			else if mode == "cx_generics" then "{reminder}"
			else if mode == "reminder" then "<i>({reminder})</i>"
			else if mode == "symbol_shortcut" then "<sym>{reminder}</sym>"
			else if mode == "rule_shortcut" then "{reminder}"
			else "<atom-reminder-{mode}><i-auto>({reminder})</i-auto></atom-reminder-{mode}>"	
		#	else keyword + if expand then "<atom-reminder-{mode}> ({reminder})</atom-reminder-{mode}>"
		}
		) +
	# step 2b : move action keywords' reminder text to the end of the line
	replace@(
		match: "(<atom-reminder-action>(?:(?!<kw-).)*</atom-reminder-action></kw[^>]*>)(((?!<atom-reminder| ?<kw-)[^\n(])+)",
		replace: "\\2\\1"
		) +
		
	# step 2c : remove duplicate reminder text
	replace@(
		match: "(<atom-reminder-[^>]*>[^)]+[)]</atom-reminder-[^>]*>)([^\n]+)\\1",
		replace: "\\2\\1"
		)  +
		
	# step 2d: Add Support for Replay Text
	replace@(
		match: "(``)([\\d\\s\\w\\'\\!\\@\\#\\$\\%\\^\\&\\*\\(\\)]+)(``)",
		replace: "<replay>\\2</replay>"
	) +
	tag_contents@(tag: "<replay>", contents: { "<b-auto>" + input + "</b-auto>" }) +
	tag_contents@(tag: "<replay>", contents: { "<color:rgba(225,25,25,240)>" + input + "</color>" }) +
		
	# step 3: revert back <kw-a>
	#tag_contents@(
	#	tag: "<kw-a>"
	#	contents: remove_kw
	#) +
	#remove_tag@(tag: "<kw-a>") +
		
	# Step 3: Add color and size
	replace@(match: "»", replace: "<size-auto:9.3><font-auto:MS Reference Sans Serif>\\0</font-auto></size-auto>") + 
	replace@(match: "«", replace: "<size-auto:9.3><font-auto:MS Reference Sans Serif>\\0</font-auto></size-auto>") +
	#replace@(match: "<i>", replace: "<size:8.9><color:rgba(33,33,33,200)>\\0") +
	#replace@(match: "</i>", replace: "\\0<color:card.rules_text.color><size: 9.7>") +
	#replace@(match: "<b>", replace: "<size-auto:8.9><color-auto:rgba(0,0,0,240)>\\0") + 		# default font: 9.4
	#replace@(match: "</b>", replace: "\\0</color></size>")
	#tag_contents@(tag: "<i", contents: { "<size:8.8>" + input + "</size>" }) +
	
	tag_contents@(tag: "<b", contents: { "<size-auto:8.75>" + input + "</size-auto>" }) +
	tag_contents@(tag: "<hidden>", contents: { "<color:rgba(0,0,0,0)>" + input + "</color>" }) +
	
	# Step 4: Add Wingdings						
	replace@(match: "\\(([1-7])\\)", replace: "<cost:\\1></cost:\\1>") +
	replace@(match: "<cost:([1-7])>", replace: "<cost:\\1><value:\\1></value:\\1>") +
	tag_contents@(tag: "<cost" contents: {
		get_cost := 	filter_text@(match: "<value:(\\d)>") 
					+	filter_text@(match: "\\d")
					+	to_int@()
		cost := get_cost(input) 
		(
		if (cost == 1) then
			"<font-auto:Wingdings 2><size-auto:11>j</size-auto></font>"
		else if (cost == 2) then
			"<font-auto:Wingdings><size-auto:11>‚</size-auto></font>"
		else if (cost == 3) then
			"<font-auto:Wingdings><size-auto:11>ƒ</size-auto></font>"
		else if (cost == 4) then
			"<font-auto:Wingdings><size-auto:11>„</size-auto></font>" 
		else if (cost == 5) then
			"<font-auto:Wingdings><size-auto:11>…</size-auto></font>"
		else if (cost == 6) then
			"<font=auto:Wingdings 2><size-auto:11>o</size-auto></font>" 	# TODO: Replace with Wingdings as it's more accurate.
		else if (cost == 7) then
			"<font-auto:Wingdings 2><size-auto:11>p</size-auto></font>"
		else
			""
		)
	}) +
	# tag_contents@(tag: "<font:Wingdings 2>", contents: { "<size-auto:11>" + input + "</size-auto>" }) +

	
	{ input }
	
	# it was 8.95 over 9.2
	# Step 4: Debug Commands
	# + trace@()


auto_italic :=
{
	input := replace(to_text(input),match: "[abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789\\.\\!\\?]", replace: "<i>\\0</i>")
}

using_sp_override :=
{
	if styling.sp_override then
		true
	else if filter_text(match: "(\\w+)", in_context:"SP override: <match>", input:card.notes) == "true" then
		true
	else if card.rarity == "SP" or card.rarity == "SSP" then
		true
	else
		false
}

card_colored_level :=
{
	if level != 0 then 
		input := "level/" + color[0] + "l" + to_string(level) + ".png"
	else 
		input := "level/l0.png"
}

card_colored_soul :=
{
	if soul == "one - 2 traits"
	then
		input := "character/" + set.side + "/" + color[0] + "1s.png"
	else if soul == "one - 1 trait"
	then
		input := "character/" + set.side + "/" + color[0] + "1s1t.png"
	else if soul == "one - no trait"
	then
		input := "character/" + set.side + "/"  + color[0] + "1s0t.png"
	else if soul ==	"two - 2 traits" 
	then 
		input := "character/" + set.side + "/" + color[0] + "2s.png"
	else if soul ==	"two - 1 trait" 
	then 
		input := "character/" + set.side + "/" + color[0] + "2s1t.png"
	else if soul ==	"two - no trait" 
	then 
		input := "character/"+ set.side + "/"  + color[0] + "2s0t.png"
	else if soul == "three - 2 traits"
	then
		input := "character/"+ set.side + "/"  + color[0] + "3s.png"
	else if soul == "three - 1 trait"
	then
		input := "character/"+ set.side + "/"  + color[0] + "3s1t.png"
	else if soul == "three - no trait"
	then
		input := "character/" + set.side + "/" + color[0] + "3s0t.png"
	else  input := "character/" + set.side + "/" + color[0] + "0s.png"
}

card_serial := 
{
	if card.rarity == "PR"
	then
		input := set.set_id + "/" + set.subset_id + "-" + "PE" + to_string(pr_card_number(), format: ".2d") + " " + rarity_or_override_rarity(next_func: rarity_or_copy)
	else if card.rarity == "(Alternate Art)" and rarity_or_copy() == "PR"
	then
		input := set.set_id + "/" + set.subset_id + "-" + "PE" + to_string(alt_pr_card_number(), format: ".2d") + " " + rarity_or_override_rarity(next_func: rarity_or_copy)
	else if card.rarity == "TD"
	then
		input := set.set_id + "/" + set.subset_id + "-TE" + to_string(td_card_number(), format: ".2d") + " " + rarity_or_override_rarity(next_func: rarity_or_copy)
	else if is_foil_or_alt_art(x: card) and rarity_or_copy() == "TD"
	then
		input := set.set_id + "/" + set.subset_id + "-TE" + to_string(alt_td_card_number(), format: ".2d") + foil_shortcut() + " " + rarity_or_override_rarity(next_func: rarity_or_copy)
	else
		input := set.set_id + "/" + set.subset_id + "-E" + to_string(card_number(), format: ".3d") + foil_shortcut() + " " + rarity_or_override_rarity(next_func: rarity_or_copy)
}

card_serial_no_rarity := 
{
	if card.rarity == "PR"
	then
		input := set.set_id + "/" + set.subset_id + "-" + "PE" + to_string(pr_card_number(), format: ".2d")
	else if card.rarity == "(Alternate Art)" and rarity_or_copy() == "PR"
	then
		input := set.set_id + "/" + set.subset_id + "-" + "PE" + to_string(alt_pr_card_number(), format: ".2d")
	else if card.rarity == "TD"
	then
		input := set.set_id + "/" + set.subset_id + "-TE" + to_string(td_card_number(), format: ".2d")
	else if is_foil_or_alt_art(x: card) and rarity_or_copy() == "TD"
	then
		input := set.set_id + "/" + set.subset_id + "-TE" + to_string(alt_td_card_number(), format: ".2d") + foil_shortcut()
	else
		input := set.set_id + "/" + set.subset_id + "-E" + to_string(card_number(), format: ".3d") + foil_shortcut()
}

rarity :=
{
	input := rarity_or_override_rarity(next_func: rarity_or_copy)
}



foil_shortcut :=
{
	if length(styling.foil_shortcut_override) > 0 then
		stlying.foil_shortcut_override
	else if card.rarity == "SP" or card.rarity == "SSP" then
		rarity_or_override_rarity(next_func: { card.rarity })
	else if card.rarity == "RRR" then
		"R"
	else if card.rarity == "SR" then
		"S"
	else
		""
}

rarity_or_override_rarity :=
{
	if length(styling.rarity_override) > 0 then
		styling.rarity_override
	else
		next_func()
}

alt_pr_card_number :=
{
	pr_card_number(card: find_original_card(card))
}

alt_td_card_number :=
{
	td_card_number(card: find_original_card(card))
}

rarity_or_copy := 
{
	if (is_foil_or_alt_art(x: card)) then # and card.rarity == "(Alternate Art)" ) then
	{
		original_card := find_original_card(card)
		if (original_card.rarity == "TD" or original_card.rarity == "PR") then
			result := original_card.rarity
		else if (card.rarity == "(Alternate Art)") then
			result := original_card.rarity
		else
			result := card.rarity
		#for each found_card in filter_list (set, filter: { (not is_foil_or_alt_art(x: input)) and input.cardname == card.cardname and using_alt_override() }) do
		#	found_card.rarity
		result
	}()
	else
		card.rarity
}

find_original_card :=
{
	found_card_list := []
	for each found_card in filter_list (set, filter: { (not is_foil_or_alt_art(x: input)) and input.cardname == card.cardname and using_alt_override() }) do
		found_card_list := [found_card]
	list_utils.last(found_card_list) or else card
}

pr_card_number :=
{
	position (
		of: card
		in: set
		order_by: { override_sort() + color_sort() + type_sort() + rarity_sort() + name_sort(card.cardname) }
		filter: { card.rarity == "PR" }
		) + 1
}

td_card_number :=
{	
	position (
		of: card
		in: set
		order_by: { override_sort() + color_sort() + type_sort() + rarity_sort() + name_sort(card.cardname) }
		filter: { card.rarity == "TD" }
		) + 1
}

override_sort :=
{
	if length(styling.serial_override) > 0 then
		styling.serial_override
	else
		filter_text(match: "(\\w+)", in_context:"override sort: <match>", input:card.notes)
}

alt_art_override :=
{
	if length(styling.alt_art_override) > 0 then
		styling.alt_art_override
	else
		filter_text(match: "(\\w+)", in_context:"alt art override: <match>", input:card.notes)
}

using_alt_override :=
{
	if alt_art_override() == "" or contains(input.serial, match: alt_art_override()) then true
	else false
}

alt_art_number :=
{
	original_card := find_original_card(card)
	result := position (
		of: original_card
		in: set
		order_by: {	main_sort() }
		filter: 
		{
			card.rarity == "RR+" or
			card.rarity == "RR" or
			card.rarity == "R" or
			card.rarity == "U" or
			card.rarity == "C" or
			card.rarity == "CC" or
			card.rarity == "CR"
		}
	) + 1
}

alt_art_number_old :=
{
	for each found_value in filter_list (set, filter: { (not is_foil_or_alt_art(x: input)) and input.cardname == card.cardname and using_alt_override() })
	do
	    result := position (
		of: found_value
		in: set
		order_by: {	main_sort() }
		filter: 
		{
			card.rarity == "RR+" or
			card.rarity == "RR" or
			card.rarity == "R" or
			card.rarity == "U" or
			card.rarity == "C" or
			card.rarity == "CC" or
			card.rarity == "CR"
		}
		) + 1
}

is_foil_or_alt_art :=
{
	x.rarity == "SP" 
	or x.rarity == "SSP" 
	or x.rarity == "RRR" 
	or x.rarity == "SR" 
	or x.rarity == "(Alternate Art)"
}

is_pr_or_td :=
{
	if x.rarity == "TD" or x.rarity == "PR"
	then true
	else false
}

is_using_legacy_card_image :=
{
	if length(styling.legacy_card_image_override) > 0 then
		styling.legacy_card_image_override == "true"
	else
		set.is_using_legacy_card_images
}

level_sort := {
	if card.level == "zero" then "000"
	else if card.level == "one" then "001"
	else if card.level == "two" then "002"
	else if card.level == "three" then "003"
	else "004"
}
cost_sort := {
	if card.cost == "zero" then "000"
	else if card.cost == "one" then "001"
	else if card.cost == "two" then "002"
	else if card.cost == "three" then "003"
	else if card.cost == "four" then "004"
	else if card.cost == "five" then "005"
	else if card.cost == "six" then "006"
	else if card.cost == "seven" then "007"
	else if card.cost == "eight" then "008"
	else if card.cost == "nine" then "009"
	else if card.cost == "ten" then "010"
	else if card.cost == "eleven" then "011"
	else if card.cost == "twelve" then "012"
	else if card.cost == "thirteen" then "013"
	else if card.cost == "fourteen" then "014"
	else if card.cost == "fifteen" then "015"
	else if card.cost == "twenty" then "020"
	else "999"
}

card_number :=
{
	if is_foil_or_alt_art(x: card)
	then
	    alt_art_number()
	else
	    position (
		of: card
		in: set
		order_by: { main_sort() }
		filter: 
		{	card.rarity == "RR+" or
			card.rarity == "RR" or
			card.rarity == "R" or
			card.rarity == "U" or
			card.rarity == "C" or
			card.rarity == "CC" or
			card.rarity == "CR"
		}
		) + 1
}

color_sort :=
{
	if card.color == "yellow" then "A"
	else if card.color == "green" then "B"
	else if card.color == "red"	then "C"
	else "D"
}
rarity_sort :=
{
	if card.rarity == "RR+" then "A"
	else if card.rarity == "RR" then "B"
	else if card.rarity == "R" then "C"
	else if card.rarity == "U" then "D"
	else if card.rarity == "C" then "E"
	else if card.rarity == "CR" then "F"
	else if card.rarity == "CC" then "G"
	else "H"
}
type_sort :=
{
	if card.card_type == "character" then "A"
	else if card.card_type == "event" then "B"
	else if card.card_type == "climax" then "C"
	else "D"
}

name_sort :=
	{ input }
		# Remove "The", "A", and "And" at the beginning
		+ replace@(match: "^(The|An?) ", replace: "")
		# Remove commas and apostrophes
		+ replace@(match: "(,|'|’)", replace: "")
		# Remove bold and italic tags
		+ replace@(match: "(<b>|<i>|</b>|</i>)", replace: "") 
		# Make lowercase
		+ to_lower
		# Replace all numbers with capital letters as number sorting is somehow broken in MSE2 (it sorts numbers last instead of letters.)
		+ replace@(match: "(0)", replace: "A")
		+ replace@(match: "(1)", replace: "B")
		+ replace@(match: "(2)", replace: "C")
		+ replace@(match: "(3)", replace: "D")
		+ replace@(match: "(4)", replace: "E")
		+ replace@(match: "(5)", replace: "F")
		+ replace@(match: "(6)", replace: "G")
		+ replace@(match: "(7)", replace: "H")
		+ replace@(match: "(8)", replace: "I")
		+ replace@(match: "(9)", replace: "J")
		+ replace@(match: "(9)", replace: "K")

main_sort := 
{
		override_sort() 
		+ 	color_sort()
		+	type_sort()
		+	rarity_sort()
		+	level_sort()
		+	cost_sort()
		+	(to_string(card.power,format: "010d") or else "0000")
		+ 	name_sort(card.cardname)			
		#+ 	to_string(sort_name(card.cardname))
}

line_height := 15.25

card_rules_height :=
{
	if (card.rules_text != "") then 
	#	max(19, ceil(card_style.rules_text.content_height or else set.stylesheet.card_style.rules_text.content_height))
		max(19, ceil(card_style.rules_text.content_lines * line_height))
	else 0
}
card_rules_height_2 :=
{
#	set.stylesheet.extra_card_style.rules_text_copy1.content_height
	set.stylesheet.extra_card_style.rules_text_copy1.content_lines * line_height
}
card_flavor_height :=
{
#	card_style.flavor_text.content_height or else set.stylesheet.card_style.flavor_text.content_height
	ceil(card_style.flavor_text.content_lines * line_height * 1.1)
}
replay_text_height :=
{
#	card_style.replay_text.content_height or else set.stylesheet.card_style.replay_text.content_height
	ceil(card_style.replay_text.content_lines * line_height)
}
replay_text_width :=
{
	(card_style.replay_text.width or else set.stylesheet.card_style.replay_text.width) - 2
}

replay_box_height :=
{
	if set.stylesheet.extra_card_style.replay_bar_1.visible == true then
		replay_text_height() + 3 * 2 + 2 + 7 
		# 3 = internal margin ; 2 = width of the box
		# 7 is the padding 4 at top and 3 at bottom
	else
		0
}

card_name_sizing := 
{
	if length(card.cardname) < 28 then
		[
			size: 15
		,	top_adjustment: 1
		]
	else if length(card.cardname) < 38 then
		[
			size: 13
		,	top_adjustment: 3
		]
	else if length(card.cardname) < 48 then
		[
			size: 11
		,	top_adjustment: 4
		]
	else
		[
			size: 09
		,	top_adjustment: 5
		]
}

ceil :=
{
	if input > to_int(input) then to_int(input) +1
	else input
}
card_climax_rules_height :=
{
	card_style.climax_rules_text.content_height or else set.stylesheet.card_style.climax_rules_text.content_height
}
card_climax_flavor_height :=
{
	card_style.climax_flavor_text.content_height or else set.stylesheet.card_style.climax_flavor_text.content_height
}
flavor_bottom :=
{
	if event then flavor_event(height: height, rules: rules)
	else flavor_chara(height: height, rules: rules)
}
flavor_event :=
{
	if rules then 547 - height
	else 557
}
flavor_chara :=
{
	if rules then 532 - height
	else 547
}
card_color_bar :=
{
	if contains(card.rarity, match: "SP") or using_sp_override() then
		input := "bars/SP_Blur/" + card.color + "bar.png"
	else
		input := "bars/" + card.color + "bar.png"
}

white_color_bar :=
{
	if contains(card.rarity, match: "SP") or using_sp_override() then
		input := "bars/SP_Blur/whitebar.png"
	else
		input := "bars/whitebar.png"
}

climax_and_character_colored_card :=
{
	if type == "character" then input := card_colored_soul(soul: soul, color: color)
	else if type == "climax" then input := "climax/" + set.side + "/"  + color + ".png"
	else if type == "event" then input := "event/" + set.side + "/" + color + ".png"
	else input := ""
}

sp_shadow :=
{
	if (contains(card.rarity, match: "SP")) then
		rgb(255,255,255)
	else
		rgba(0,0,0,0)
}